# $Id: PKGBUILD,v 1.85 2008/03/30 20:25:40 jason Exp $
# Contributer: Jason Chu <jason@archlinux.org>
# Maintainer: Jason Chu <jason@archlinux.org>
pkgname=subversion
pkgver=1.4.6
pkgrel=1
pkgdesc="Replacement for CVS, another versioning system (svn)"
arch=(i686 x86_64)
license=('apache' 'bsd')
depends=('neon>=0.27.2-1' 'apr-util>=1.2.12-1')
makedepends=('heimdal>=1.0.1' 'db>=4.6' 'apache' 'python>=2.5'
             'perl>=5.10.0' 'swig>=1.3.27' 'jdk' 'jre' 'ruby'
             'autoconf')
source=(http://svn.collab.net/tarballs/${pkgname}-${pkgver}.tar.gz
        svnserve svn svnserve.conf subversion-neon.patch)
md5sums=('32786fe2f322982c0567346de18f6460'
         'c5a2e490b3e58c6bd2ca3a2e72b71845'
         'a0db6dd43af33952739b6ec089852630'
         'c459e299192552f61578f3438abf0664'
         'f869289684b394840e10908c89460303')
backup=('etc/xinetd.d/svn' 'etc/conf.d/svnserve')
url="http://subversion.tigris.org/"
provides=('svn')
options=('!makeflags' 'libtool')

build() {
   cd $startdir/src/${pkgname}-${pkgver}

   [ -z "${J2REDIR}" ] && . /etc/profile.d/jre.sh
   [ -z "${J2SDKDIR}" ] && . /etc/profile.d/jdk.sh

   # patch from gentoo
   patch -Np1 -i $startdir/src/subversion-neon.patch

   sed -e 's/\(NEON_ALLOWED_LIST=.* 0.26.4\)"/\1 0.27.2 0.28.0 0.28.1"/' -i configure.in

   libtoolize --force --copy
   aclocal -I build/ac-macros
   autoconf
   ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr --with-zlib --with-neon=/usr --with-apxs --enable-javahl --with-jdk=/opt/java

   (make external-all && make LT_LDFLAGS="-L$Fdestdir/usr/lib" local-all ) || return 1

   export LD_LIBRARY_PATH=$startdir/pkg/usr/lib:$LD_LIBRARY_PATH
   make DESTDIR=$startdir/pkg install || return 1

   make DESTDIR=$startdir/pkg swig-py || return 1
   make install-swig-py DESTDIR=$startdir/pkg || return 1

   mkdir -p $startdir/pkg/usr/lib/python2.5
   mv $startdir/pkg/usr/lib/svn-python/ $startdir/pkg/usr/lib/python2.5/site-packages


   mkdir -p $startdir/pkg/usr/share/subversion
   install -d -m 755 tools/hook-scripts $startdir/pkg/usr/share/subversion/
   rm -f $startdir/pkg/usr/share/subversion/hook-scripts/*.in

   make DESTDIR=$startdir/pkg swig-pl || return 1
   make install-swig-pl DESTDIR=$startdir/pkg INSTALLDIRS=vendor || return 1
   rm -f $startdir/pkg/usr/lib/perl5/vendor_perl/auto/SVN/_Core/.packlist
   rm -rf $startdir/pkg/usr/lib/perl5/core_perl

   make DESTDIR=$startdir/pkg swig-rb || return 1
   make install-swig-rb DESTDIR=$startdir/pkg  || return 1

   make DESTDIR=$startdir/pkg javahl || return 1
   make DESTDIR=$startdir/pkg install-javahl || return 1

   mkdir -p $startdir/pkg/etc/rc.d
   mkdir -p $startdir/pkg/etc/xinetd.d
   mkdir -p $startdir/pkg/etc/conf.d

   install -m 755 $startdir/src/svnserve $startdir/pkg/etc/rc.d
   install -m 644 $startdir/src/svn $startdir/pkg/etc/xinetd.d
   install -m 644 $startdir/src/svnserve.conf $startdir/pkg/etc/conf.d/svnserve
   install -m 755 $startdir/src/subversion-$pkgver/contrib/client-side/svnmerge.py $startdir/pkg/usr/bin/svnmerge
   install -D -m 644 $startdir/src/subversion-$pkgver/COPYING $startdir/pkg/usr/share/licenses/$pkgname/LICENSE

#libtoolslay not all because of kdesdk
   find ${startdir}/pkg/usr/lib/apache -name '*.la' -exec rm {} \;
   find ${startdir}/pkg/usr/lib/ruby -name '*.la' -exec rm {} \;
   find ${startdir}/pkg/usr/lib/python2.5 -name '*.la' -exec rm {} \;
}
