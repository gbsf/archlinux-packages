# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10-good-plugins
pkgver=0.10.7
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good)"
arch=('i686' 'x86_64')
license=('LGPL')
depends=("gstreamer0.10-good>=${pkgver}" 'libavc1394' 'hal>=0.5.10' 'libiec61883' 'aalib' 'libshout' 'libdv' 'esd' 'libcdio>=0.80' 'flac' 'gconf>=2.22.0' 'wavpack' 'taglib')
makedepends=('perlxml' 'pkgconfig')
replaces=('gstreamer0.10-aalib' 'gstreamer0.10-wavpack' 'gstreamer0.10-shout2' 'gstreamer0.10-taglib' 'gstreamer0.10-libcaca' 'gstreamer0.10-libpng' 'gstreamer0.10-jpeg' 'gstreamer0.10-cairo' 'gstreamer0.10-flac' 'gstreamer0.10-speex' 'gstreamer0.10-gdkpixbuf' 'gstreamer0.10-hal' 'gstreamer0.10-dv1394' 'gstreamer0.10-annodex' 'gstreamer0.10-gconf' 'gstreamer0.10-esd' 'gstreamer0.10-cdio' 'gstreamer0.10-dv')
conflicts=('gstreamer0.10-aalib' 'gstreamer0.10-wavpack' 'gstreamer0.10-shout2' 'gstreamer0.10-taglib' 'gstreamer0.10-libcaca' 'gstreamer0.10-libpng' 'gstreamer0.10-jpeg' 'gstreamer0.10-cairo' 'gstreamer0.10-flac' 'gstreamer0.10-speex' 'gstreamer0.10-gdkpixbuf' 'gstreamer0.10-hal' 'gstreamer0.10-dv1394' 'gstreamer0.10-annodex' 'gstreamer0.10-gconf' 'gstreamer0.10-esd' 'gstreamer0.10-cdio' 'gstreamer0.10-dv')
provides=("gstreamer0.10-aalib=${pkgver}" "gstreamer0.10-wavpack=${pkgver}" "gstreamer0.10-shout2=${pkgver}" "gstreamer0.10-taglib=${pkgver}" "gstreamer0.10-libcaca=${pkgver}" "gstreamer0.10-libpng=${pkgver}" "gstreamer0.10-jpeg=${pkgver}" "gstreamer0.10-cairo=${pkgver}" "gstreamer0.10-flac=${pkgver}" "gstreamer0.10-speex=${pkgver}" "gstreamer0.10-gdkpixbuf=${pkgver}" "gstreamer0.10-hal=${pkgver}" "gstreamer0.10-dv1394=${pkgver}" "gstreamer0.10-annodex=${pkgver}" "gstreamer0.10-gconf=${pkgver}" "gstreamer0.10-esd=${pkgver}" "gstreamer0.10-cdio=${pkgver}" "gstreamer0.10-dv=${pkgver}" "gst-plugins-good=${pkgver}")
url="http://gstreamer.freedesktop.org/"
groups=('gstreamer0.10-plugins')
options=(!libtool)
_relname=gst-plugins-good
source=(${url}/src/${_relname}/${_relname}-${pkgver}.tar.bz2)
md5sums=('e02d729b22451f75414a27456ae5c16a')

build() {
  cd ${startdir}/src/${_relname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-docs-build --disable-plugin-docs \
    --enable-ladspa \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  for dir in ext sys/oss sys/ximage; do
    make -C ${dir} DESTDIR=${startdir}/pkg install || return 1
  done
}
