# $Id: PKGBUILD,v 1.51 2008/03/05 07:47:10 tpowa Exp $
# Maintainer: damir <damir@archlinux.org>
# Maintainer: Travis Willard <travis@archlinux.org> 
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=kdevelop
pkgver=3.5.1
pkgrel=1.1
pkgdesc="A C/C++ development environment for KDE - stable snapshot build"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs>=3.5.9' 'kdebase>=3.5.9' 'kdesdk>=3.5.9' 'flex>=2.5.4' 'gcc' 'perl' 'make' 
         'automake>=1.6' 'autoconf>=2.52' 'gettext' 'db>=4.6' 'cvs' 'subversion')
replaces=('kdevelop3-cvs') # compatibility reasons
conflicts=('kdevelop-devel')

# for easier build, just uncomment the mirror you want to use --- remember: source is about 6MB!
  mirror="ftp.solnet.ch/mirror/KDE"         # updated every 2 hours, very fast for Europe
# mirror="ftp.kde.org/pub/kde/"             # main server
# mirror="ibiblio.org/pub/mirrors/kde/"     # ibiblio mirror

source=(ftp://$mirror/stable/$pkgname-latest/src/$pkgname-$pkgver.tar.bz2 post-3.5.9.patch)

build() {
   # Source the QT and KDE profile
   [ "$QTDIR" = "" ] && source /etc/profile.d/qt3.sh 
   [ "$KDEDIR" = "" ] && source /etc/profile.d/kde.sh
   # start building
   cd ${startdir}/src/${pkgname}-${pkgver}
   patch -Np0 -i ../post-3.5.9.patch || return 1
   ./configure --prefix=/opt/kde --enable-gcc-hidden-visibility || return 1
   make || return 1
   make DESTDIR=$startdir/pkg install || return 1
   rm -f ${startdir}/pkg/opt/kde/bin/extractrc
}
md5sums=('80d2216a0089fe142735d34ae8de6a0c'
         '9a5e518c257638d38e273dd345148ff2')
