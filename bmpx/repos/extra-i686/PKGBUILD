# $Id$
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: alzen <mynick2(at)gmail(dot)com>

pkgname=bmpx
pkgver=0.40.13
pkgrel=1
pkgdesc="A media player and the successor to BMP"
arch=(i686 x86_64)
url="http://beep-media-player.org/"
depends=('boost' 'fam' 'gstreamer0.10-alsa' 'gstreamer0.10-cdparanoia'
	'gstreamer0.10-good' 'gstreamer0.10-vorbis' 'hal' 'libglademm' 'libofa'
	'librsvg' 'libsexymm' 'libsoup' 'sqlite3' 'startup-notification>=0.8'
	'taglib')
# 0.40.0: bmpx provide their own neon and musicbrainz implementation.
makedepends=('bison' 'flex' 'pkgconfig')
options=('!libtool')
license=('GPL' 'custom')
install=bmpx.install
source=(http://files.beep-media-player.org/releases/0.40/${pkgname}-${pkgver}.tar.bz2)
md5sums=('11da3d4c63e77dc15a53d0c6c25ddadb')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-amazon --libexecdir=/usr/lib/bmpx
  sed -e '/BUILD_BUILDUSER/ s@".*"@"http://www.archlinux.org"@' -i build.h
  make || return 1
  make DESTDIR=${startdir}/pkg install

  # install license
  install -D -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/LICENSE

  #Fix invalid symlink pointing to builddir
  cd ${startdir}/pkg/usr/bin
  rm -f bmp2
  ln -sf beep-media-player-2 bmp2
}
