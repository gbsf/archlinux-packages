# $Id$
# Maintainer: Daniel Isenmann <daniel @ archlinux.org>
# Contributor: Shadowhand <woody.gilk@gmail.com>
pkgname=beagle
pkgver=0.3.6.1
pkgrel=2
pkgdesc="Search tool that ransacks your personal information space"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/beagle/"
license=('custom')
depends=('gnome-sharp' 'gmime>=2.2.18' 'evolution-sharp>=0.16.1' 'libexif' 'librsvg>=2.22.2' 'sqlite3' 'libxss' 'wv>=1.2.4' 'chmlib' 'gsf-sharp>=0.8.1' 'libsm' 'xdg-utils' 'ndesk-dbus-glib' 'mono>=1.9')
makedepends=('perlxml' 'zip' 'pkgconfig')
options=('!libtool')
backup=('etc/beagle/crawl-applications' 'etc/beagle/crawl-documentation'
        'etc/beagle/crawl-windows')
install=beagle.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.3/${pkgname}-${pkgver}.tar.bz2
	beagle-0.2.1-spew.patch)
md5sums=('b2ef90b36bed0a24b7ccf52d388c5b98'
         '74feeb60c70999db8895b3a9d260869a')

build() {
  export MONO_SHARED_DIR=${startdir}/src/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd ${startdir}/src/${pkgname}-${pkgver}
  sed -i -e 's/CRAWL_USER=beagleindex/CRAWL_USER=beaglidx/' tools/beagle-crawl-system.in
  sed -i -e 's/libchm.so.1/libchm.so.0/' Util/ChmFile.cs
  patch -Np1 -i ${startdir}/src/beagle-0.2.1-spew.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-inotify \
	--disable-libbeagle \
	--disable-static \
	--disable-docs \
        --disable-evolution	
  make || return 1
  make DESTDIR=${startdir}/pkg install

  mkdir -p ${startdir}/pkg/var/cache/beagle/indexes
  chown -R beaglidx:beaglidx ${startdir}/pkg/var/cache/beagle
  mkdir -p ${startdir}/pkg/usr/share/licenses/beagle
  install -m644 ${startdir}/src/${pkgname}-${pkgver}/COPYING \
                ${startdir}/pkg/usr/share/licenses/beagle
  chown -R beaglidx:beaglidx ${startdir}/pkg/var/cache/beagle
}
